#version 450
/* Copyright (c) 2020 Themaister
 *
 * Permission is hereby granted, free of charge, to any person obtaining
 * a copy of this software and associated documentation files (the
 * "Software"), to deal in the Software without restriction, including
 * without limitation the rights to use, copy, modify, merge, publish,
 * distribute, sublicense, and/or sell copies of the Software, and to
 * permit persons to whom the Software is furnished to do so, subject to
 * the following conditions:
 *
 * The above copyright notice and this permission notice shall be
 * included in all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
 * EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
 * MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
 * IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
 * CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
 * TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
 * SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 */

layout(local_size_x_id = 0) in;
layout(constant_id = 1) const int PAGE_STRIDE = 256;

layout(set = 0, binding = 0, std430) buffer RDRAM
{
    uint rdram[];
};

layout(set = 0, binding = 1, std430) readonly buffer StagingRDRAM
{
    uint staging_rdram[];
};

layout(set = 0, binding = 2, std430) readonly buffer WriteMaskRDRAM
{
    uint writemask[];
};

layout(set = 1, binding = 0, std140) uniform UBO
{
    uvec4 offsets[1024];
};

void main()
{
    uint offset = offsets[gl_WorkGroupID.x >> 2u][gl_WorkGroupID.x & 3u];
    offset *= PAGE_STRIDE;
    offset += gl_LocalInvocationIndex;
    uint mask = writemask[offset];

    if (mask == ~0u)
    {
        return;
    }
    else if (mask == 0u)
    {
        uint staging = staging_rdram[offset];
        rdram[offset] = staging;
    }
    else
    {
        uint word = rdram[offset];
        uint staging = staging_rdram[offset];
        word = (word & mask) | (staging & ~mask);
        rdram[offset] = word;
    }
}